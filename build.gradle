plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.2'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://maven.citizensnpcs.co/repo' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.22'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'

    compileOnly('net.citizensnpcs:citizens-main:2.0.30-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    compileOnly fileTree('libs')

    implementation 'com.github.HenryFabio:inventory-api:main-SNAPSHOT'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'com.github.Yuhtin:update-checker:1.2'

    def lombok = 'org.projectlombok:lombok:1.18.24'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.stores.NextStores'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    apiVersion = '1.13'

    softDepend = ['PlotSquared', 'HolographicDisplays', 'Citizens', 'CMI']

    load = 'POSTWORLD'

    commands {
        store {
            permission = 'nextstores.command.store'
            aliases = ['lojas', 'stores']
        }
        storesetnpc {
            permission = 'nextstores.command.setstore'
            aliases = ['lojassetnpc']
        }
        storedeletenpc {
            permission = 'nextstores.command.setstore'
            aliases = ['storedelnpc', 'lojadelnpc']
        }
    }
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.stores.libs.inventoryapi'
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.stores.libs.configinjector'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.stores.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.stores.libs.hikari'

}

compileJava {
    options.encoding = 'UTF-8'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}